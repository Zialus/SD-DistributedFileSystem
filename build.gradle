plugins {
    id 'info.solidsoft.pitest' version '1.7.4'
    id "se.patrikerdes.use-latest-versions" version "0.2.18"
    id "com.github.ben-manes.versions" version "0.42.0"
    id "net.ltgt.errorprone" version "2.0.2"
    id "com.github.spotbugs" version "5.0.9"
    id "pmd"
//    id 'checkstyle'
    id "java"
    id "idea"
    id 'jacoco'
    id "org.sonarqube" version "3.4.0.2513"
    id "io.freefair.lombok" version "6.5.0.3"
}

sonarqube {
    properties {
        property "sonar.projectKey", "Zialus_SD-DistributedFileSystem"
        property "sonar.organization", "zialus-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

group 'fcup'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'

    implementation 'org.jline:jline:3.21.0'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'

    annotationProcessor "com.uber.nullaway:nullaway:0.9.9"
    errorprone 'com.google.errorprone:error_prone_core:2.15.0'
}

tasks.withType(JavaCompile) {
    options.errorprone {
        option("NullAway:AnnotatedPackages", "fcup")
    }
    options.compilerArgs += ['-Xlint:all']
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.required = true
    }
}

// Don't produce default Jar
jar.enabled = false

def jarPackage(String groupName, String artifactName, String artifactVersion) {
    return tasks.create("jar_${artifactName}", Jar) {
        getArchiveBaseName().set(artifactName)
        getArchiveVersion().set(artifactVersion)

        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

        from(sourceSets.main.output) {
            include "$groupName/**"
        }

        manifest {
            attributes(
                    'Main-Class': "$groupName.$artifactName"
            )
        }

    }
}

artifacts {
    archives jarPackage("$group", "StorageServer", "$version"),
            jarPackage("$group", "Client", "$version"),
            jarPackage("$group", "MetadataServer", "$version")
}

pmd {
    ignoreFailures = true
}

pmdMain {
    reports {
        xml.required = false
        html.required = true
    }
}

pmdTest {
    reports {
        xml.required = false
        html.required = true
    }
}

pitest {
    junit5PluginVersion = '1.0.0'
    pitestVersion = '1.9.4'
    mutators = ["ALL"]
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

lombok {
    version = '1.18.24'
}

spotbugs {
    toolVersion = '4.7.1'
    ignoreFailures = true
    showStackTraces = false
    showProgress = true
}

spotbugsMain {
    reports {
        xml.required = false
        html.required = true
    }
}

spotbugsTest {
    reports {
        xml.required = false
        html.required = true
    }
}
