plugins {
    id 'info.solidsoft.pitest' version '1.4.8'
    id "se.patrikerdes.use-latest-versions" version "0.2.13"
    id "com.github.ben-manes.versions" version "0.28.0"
    id "net.ltgt.errorprone" version "1.1.1"
    id "com.github.spotbugs" version "4.0.5"
    id "pmd"
//    id 'checkstyle'
    id "java"
    id "idea"
    id 'jacoco'
    id "org.sonarqube" version "2.8"
}

sonarqube {
    properties {
        property "sonar.projectKey", "Zialus_SD-DistributedFileSystem"
    }
}

group 'fcup'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.1'

    implementation 'org.jline:jline:3.14.0'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'

    errorprone 'com.google.errorprone:error_prone_core:2.3.4'
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.enabled = true
    }
}

// Don't produce default Jar
jar.enabled = false

def jarPackage(String groupName, String artifactName, String artifactVersion) {
    return tasks.create("jar_${artifactName}", Jar) {
        getArchiveBaseName().set(artifactName)
        getArchiveVersion().set(artifactVersion)

        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

        from(sourceSets.main.output) {
            include "$groupName/**"
        }

        manifest {
            attributes(
                    'Main-Class': "$groupName.$artifactName"
            )
        }

    }
}

artifacts {
    archives jarPackage("$group", "StorageServer", "$version"),
            jarPackage("$group", "Client", "$version"),
            jarPackage("$group", "MetadataServer", "$version")
}

pmd {
    ignoreFailures = true
}

pmdMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmdTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pitest {
    junit5PluginVersion = '0.12'
    pitestVersion = '1.5.1'
    mutators = ["ALL"]
}

jacocoTestReport {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugs {
    toolVersion = '4.0.1'
    ignoreFailures = true
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
