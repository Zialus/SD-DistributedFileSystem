plugins {
    id "net.ltgt.errorprone" version "0.0.13"
    id "net.ltgt.apt" version "0.15"
    id "java"
    id "idea"
}

group 'fcup'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.1'

    compile 'org.jline:jline:3.6.1'

    apt "com.uber.nullaway:nullaway:0.3.4"

    // Optional, some source of nullability annotations.
    // Not required on Android if you use the support
    // library nullability annotations.
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"

    errorprone "com.google.errorprone:error_prone_core:2.2.0"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.enabled = true
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fcup.Editor'
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

// Don't produce default Jar
jar.enabled = false

def jarPackage(groupName, artifactName, artifactVersion) {
    return tasks.create("jar${artifactName}", Jar) {
        baseName = artifactName
        version = artifactVersion
        String group = groupName

        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

        from(sourceSets.main.output) {
            include "$groupName/**"
        }

        manifest {
            attributes(
                    'Specification-Title': "$baseName",
                    'Specification-Version': "$version",
                    'Main-Class': "$group.$baseName"
            )
        }

    }
}

artifacts {
    archives jarPackage("$group", "StorageServer", "$version"),
            jarPackage("$group", "Client", "$version"),
            jarPackage("$group", "MetadataServer", "$version")
}

tasks.withType(JavaCompile) {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.compilerArgs += ["-Xep:NullAway:ERROR", "-XepOpt:NullAway:AnnotatedPackages=com.uber"]
    }
}
