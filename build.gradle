plugins {
    id 'info.solidsoft.pitest' version '1.6.0'
    id "se.patrikerdes.use-latest-versions" version "0.2.15"
    id "com.github.ben-manes.versions" version "0.38.0"
    id "net.ltgt.errorprone" version "2.0.1"
    id "com.github.spotbugs" version "4.7.0"
//    id "pmd"
//    id 'checkstyle'
    id "java"
    id "idea"
    id 'jacoco'
    id "org.sonarqube" version "3.1.1"
    id "io.freefair.lombok" version "5.3.3.3"
}

sonarqube {
    properties {
        property "sonar.projectKey", "Zialus_SD-DistributedFileSystem"
        property "sonar.organization", "zialus-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

group 'fcup'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.0-M1'

    implementation 'org.jline:jline:3.19.0'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'

    annotationProcessor "com.uber.nullaway:nullaway:0.9.1"
    errorprone 'com.google.errorprone:error_prone_core:2.6.0'
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

tasks.withType(JavaCompile) {
    options.errorprone {
        option("NullAway:AnnotatedPackages", "fcup")
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        html.enabled = true
    }
}

// Don't produce default Jar
jar.enabled = false

def jarPackage(String groupName, String artifactName, String artifactVersion) {
    return tasks.create("jar_${artifactName}", Jar) {
        getArchiveBaseName().set(artifactName)
        getArchiveVersion().set(artifactVersion)

        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

        from(sourceSets.main.output) {
            include "$groupName/**"
        }

        manifest {
            attributes(
                    'Main-Class': "$groupName.$artifactName"
            )
        }

    }
}

artifacts {
    archives jarPackage("$group", "StorageServer", "$version"),
            jarPackage("$group", "Client", "$version"),
            jarPackage("$group", "MetadataServer", "$version")
}

//pmd {
//    ignoreFailures = true
//}
//
//pmdMain {
//    reports {
//        xml.enabled = false
//        html.enabled = true
//    }
//}
//
//pmdTest {
//    reports {
//        xml.enabled = false
//        html.enabled = true
//    }
//}

pitest {
    junit5PluginVersion = '0.14'
    pitestVersion = '1.6.4'
    mutators = ["ALL"]
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

lombok {
    version = '1.18.20'
}

spotbugs {
    toolVersion = '4.2.2'
    ignoreFailures = true
    showStackTraces = false
    showProgress = true
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
